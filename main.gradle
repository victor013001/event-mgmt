apply plugin: 'info.solidsoft.pitest.aggregator'

allprojects {
    repositories {
        //mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'info.solidsoft.pitest'

    compileJava.dependsOn validateStructure

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    //build.dependsOn 'pitest'

    test {
        useJUnitPlatform()
    }

    dependencies {
        //REACTOR
        implementation 'io.projectreactor:reactor-core'
        implementation 'io.projectreactor.addons:reactor-extra'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.15.RELEASE'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        //SPRING
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        //UTIL
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        //AWS
        implementation platform("software.amazon.awssdk:bom:${awsSdkVersion}")

        if (!project.name.equalsIgnoreCase(modelModule) && !project.name.equalsIgnoreCase(useCaseModule)) {
            //LOGGING
            dependencies.add("implementation", "ch.qos.logback:logback-classic:${logbackClasicVersion}")
            dependencies.add("implementation", "net.logstash.logback:logstash-logback-encoder:${logstashVersion}")
        }
    }

    tasks.withType(Test).configureEach {
        if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_13)) {
            jvmArgs += [
                    "-XX:+AllowRedefinitionToAddDeleteMethods"
            ]
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    pitest {
        targetClasses = ['co.com.techtest.*']
        excludedClasses = []
        excludedTestClasses = []
        pitestVersion = '1.22.0'
        verbose = false
        outputFormats = ['XML', 'HTML']
        threads = 8
        exportLineCoverage = true
        useClasspathFile = true
        withHistory = true
        timestampedReports = false
        //mutators = ['STRONGER', 'DEFAULTS']
        fileExtensionsToFilter.addAll('xml', 'orbit')
        junit5PluginVersion = '1.2.3'
        failWhenNoMutations = false
        jvmArgs = ["-XX:+AllowRedefinitionToAddDeleteMethods"]
    }

    jacocoTestReport {
        dependsOn test, 'pitest'
        reports {
            xml.setRequired true
            xml.setOutputLocation layout.buildDirectory.file("reports/jacoco.xml")
            csv.setRequired false
            html.setOutputLocation layout.buildDirectory.dir("reports/jacocoHtml")
        }
    }

}

jacoco {
    toolVersion = "${jacocoVersion}"
    reportsDirectory.set(layout.buildDirectory.dir("reports"))
}

tasks.register('jacocoMergedReport', JacocoReport) {
    dependsOn = [test, subprojects.jacocoTestReport, pitestReportAggregate]
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.setRequired true
        csv.setRequired false
        html.setRequired true
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                '**/deployment/**',
                '**/applications/**',
                '**/config/**',
                '**/exception/**',
                '**/MainApplication.class',
                '**/mapper/**',
                '**/repository/**',
        ])
    }))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}

pitestReportAggregate {
    doLast {
        def reportDir = layout.buildDirectory.dir("reports/pitest").get().asFile
        def consolidatedReport = new File(reportDir, 'mutations.xml')
        consolidatedReport.withWriter { writer ->
            writer.write("<mutations>\n")
            subprojects.each { subproject ->
                def xmlReport = subproject.layout.buildDirectory.file("reports/pitest/mutations.xml").get().asFile
                if (xmlReport.exists()) {
                    def xmlContent = xmlReport.text
                    xmlContent = xmlContent.replaceAll("<\\?xml[^>]*>", "")
                    xmlContent = xmlContent.replaceAll("</?mutations( partial=\"true\")?>", "")
                    writer.write(xmlContent.trim() + "\n")
                }
            }
            writer.write("</mutations>")
        }
    }
}

tasks.named('wrapper') {
    gradleVersion = '9.2.1'
}
